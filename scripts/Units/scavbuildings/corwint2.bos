#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, fan, cradle;

static-var  WindSpeed, WindDirection, LastDirection, Built, Stunned;


Go()
{
	sleep RAND(1, 500);
	
	//Take 7s to turn and accelerate
	//Angular speed is always positive, even if the angle is negative. The speed is in Spring angles/second (sa/s). 
	var DirectionDelta;
	DirectionDelta = WindDirection - LastDirection;
	if (DirectionDelta < 0 ) DirectionDelta = 0 - DirectionDelta;
	if (DirectionDelta > <180>) DirectionDelta = DirectionDelta - <180>;
	//get PRINT(get GAME_FRAME,DirectionDelta, WindSpeed, WindDirection);
	
	turn cradle to y-axis WindDirection speed DirectionDelta/7;
	spin fan around z-axis speed WindSpeed accelerate <1.0>;
	//The angular acceleration should always be positive, even if you are spinning the piece at a negative angular speed. 
	//It is measured in Spring angles per frame-second (sa/f*s); in other words, 
	//the speed of the piece will increase by that amount every frame, not every second. There are 30 frames per second. 
	//spin [piece] around [axis] speed [angular speed] accelerate [angular acceleration];
	
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	Built = 1;
	call-script Go();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}


Create()
{
	WindSpeed = 800;
	WindDirection = 0;
	LastDirection = 0;
	Built = 0;
	start-script SmokeUnit();
}

SetSpeed(windSpd) //called every 450 frames
{
	WindSpeed = windSpd; //invert if desired
	if (!Stunned) {
		if(Built)
		{
			set ACTIVATION to 100;
		}
	}
}

SetDirection(windDir) //called every 450 frames
{
	LastDirection = WindDirection;
	WindDirection = windDir;
	
	if (!Stunned) {
		if(Built)
		{
			call-script Go();
		}
	}
}

Stop()
{
	stop-spin fan around z-axis decelerate <10.0>;
}

SetStunned(State)
{
	Stunned = State;
	if (State) {
		call-script Stop();
		set ACTIVATION to 0;
	}else{
		call-script Go();
		set ACTIVATION to 100;
	}
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cradle type BITMAPONLY | NOHEATCLOUD;
		explode fan type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cradle type BITMAPONLY | NOHEATCLOUD;
		explode fan type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cradle type BITMAPONLY | NOHEATCLOUD;
		explode fan type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cradle type BITMAPONLY | NOHEATCLOUD;
		explode fan type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
