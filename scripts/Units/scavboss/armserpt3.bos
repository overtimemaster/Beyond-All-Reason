#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  lturret, rturret, llturret, rrturret, base, lprop, rprop, bprop;

static-var  turnClock, turnCounter, wheelSpeed, currentSpeed, moveSpeed, pivotAngle;

// Signal definitions
#define SIG_MOVE			2

#define SIG_AIM				4
#define SIG_AIM_2			8
#define SIG_AIM_3			16
#define SIG_AIM_4			32
#define SIG_AIM_5			64


#define BASEPIECE base
#define HITSPEED <25.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 200
HitByWeapon(anglex, anglez, damage)	// angle[x|z] is always [-500;500], damage is multiplied by 100
{
	var amount;//, speedz, speedx;
	amount = damage / (100 * UNITSIZE);
	if (amount < 3  ) return (0);
	if (amount > MAXTILT) amount = MAXTILT;
	//get PRINT(anglex, anglez, amount, damage);
	//speedz = HITSPEED * get ABS(anglez) / 500; //nevermind this, the random error this produces actually looks better than the accurate version
	turn BASEPIECE to z-axis (anglez * amount) / 100  speed HITSPEED;
	turn BASEPIECE to x-axis <0> - (anglex * amount) /100 speed HITSPEED;
	wait-for-turn BASEPIECE around z-axis;
	wait-for-turn BASEPIECE around x-axis;
	turn BASEPIECE to z-axis <0.000000> speed HITSPEED / 4;
	turn BASEPIECE to x-axis <0.000000> speed HITSPEED / 4;
}
HitByWeaponId(anglex, anglez, weaponid, dmg) //weaponID is always 0,lasers and flamers give angles of 0
{
	start-script HitByWeapon(dmg, anglez,anglex); //I dont know why param order must be switched, and this also runs a frame later :(
	return (100); //return damage percent
}

TurnControl()
{
	var lastHeading, currHeading, diffHeading;
	lastHeading = GET HEADING;
	while (TRUE)
	{
		currHeading = GET HEADING;
		diffHeading = currHeading - lastHeading;
		// Set values for tilt
		turnClock = 1;
		turnCounter = 1;	
		if (diffHeading >= 500)
			{
			turnClock = <2>;
			}
		if (diffHeading <= -500)
			{
			turnCounter = <2>;
			}
		lastHeading = currHeading;
		sleep 67;
	}
}

Propcontrol()
{
	while(TRUE)
	{
		currentSpeed = (get CURRENT_SPEED)*1000/moveSpeed;
		spin lprop around z-axis speed (<-1.000000> * currentSpeed ) * turnCounter accelerate <10.000000>;
		spin rprop around z-axis speed (<1.000000> * currentSpeed ) * turnClock accelerate <10.000000>;
		spin bprop around z-axis speed <1.000000>*currentSpeed accelerate <10.000000>;
		sleep 33;
	}
}

Create()
{
	moveSpeed = get MAX_SPEED;
	start-script Propcontrol();
	start-script TurnControl();
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;

	while( TRUE )
	{
		emit-sfx 256 | 3 from lprop;
		emit-sfx 256 | 3 from rprop;
		emit-sfx 256 | 3 from bprop;
		sleep 250;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	move rturret to x-axis [0] speed [10];
	move lturret to x-axis [0] speed [10];
	move rrturret to x-axis [0] speed [10];
	move llturret to x-axis [0] speed [10];

	wait-for-move rturret along x-axis;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep 5000;
	start-script ExecuteRestoreAfterDelay();
}

QueryWeapon1(piecenum)
{
	piecenum = base;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	return (1);
}

AimFromWeapon1(piecenum)
{
	piecenum = base;
}

QueryWeapon2(piecenum)
{
	piecenum = lturret;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	move lturret to x-axis [-9] speed [30];
	move llturret to x-axis [-9] speed [30];
	wait-for-move lturret along x-axis;
	wait-for-move llturret along x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

Shot2()
{
	return (1);
}

AimFromWeapon2(piecenum)
{
	piecenum = lturret;
}

QueryWeapon3(piecenum)
{
	piecenum = llturret;
}

AimWeapon3(heading, pitch)
{
	signal SIG_AIM_3;
	set-signal-mask SIG_AIM_3;
	move lturret to x-axis [-9] speed [30];
	move llturret to x-axis [-9] speed [30];
	wait-for-move lturret along x-axis;
	wait-for-move llturret along x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

Shot3()
{
	return (1);
}

AimFromWeapon3(piecenum)
{
	piecenum = llturret;
}

QueryWeapon4(piecenum)
{
	piecenum = rturret;
}

AimWeapon4(heading, pitch)
{
	signal SIG_AIM_4;
	set-signal-mask SIG_AIM_4;
	move rturret to x-axis [9] speed [30];
	move rrturret to x-axis [9] speed [30];
	wait-for-move rturret along x-axis;
	wait-for-move rrturret along x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

Shot4()
{
	return (1);
}

AimFromWeapon4(piecenum)
{
	piecenum = rturret;
}

QueryWeapon5(piecenum)
{
	piecenum = rrturret;
}

AimWeapon5(heading, pitch)
{
	signal SIG_AIM_5;
	set-signal-mask SIG_AIM_5;
	move rturret to x-axis [9] speed [30];
	move rrturret to x-axis [9] speed [30];
	wait-for-move rturret along x-axis;
	wait-for-move rrturret along x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

Shot5()
{
	return (1);
}

AimFromWeapon5(piecenum)
{
	piecenum = rrturret;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode rprop type FIRE | SMOKE | NOHEATCLOUD;
		explode rturret type BITMAPONLY | NOHEATCLOUD;
		explode lprop type BITMAPONLY | NOHEATCLOUD;
		explode lturret type BITMAPONLY | NOHEATCLOUD;
		explode bprop type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode rprop type FIRE | SMOKE | NOHEATCLOUD;
		explode rturret type FIRE | SMOKE | NOHEATCLOUD;
		explode lprop type FIRE | SMOKE | NOHEATCLOUD;
		explode lturret type FALL | NOHEATCLOUD;
		explode bprop type FIRE | SMOKE | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode rprop type EXPLODE_ON_HIT | FIRE | SMOKE | NOHEATCLOUD;
		explode rturret type SMOKE | FALL | NOHEATCLOUD;
		explode lprop type SMOKE | FALL | NOHEATCLOUD;
		explode lturret type EXPLODE_ON_HIT | SMOKE | NOHEATCLOUD;
		explode bprop type EXPLODE_ON_HIT | SMOKE | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode rprop type EXPLODE_ON_HIT | FIRE | NOHEATCLOUD;
		explode rturret type EXPLODE_ON_HIT | FIRE | NOHEATCLOUD;
		explode lprop type EXPLODE_ON_HIT | FIRE | NOHEATCLOUD;
		explode lturret type EXPLODE_ON_HIT | FIRE | SMOKE | NOHEATCLOUD;
		explode bprop type EXPLODE_ON_HIT | FIRE | SMOKE | NOHEATCLOUD;
	return corpsetype;
}
